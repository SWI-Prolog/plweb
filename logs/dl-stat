#!/home/swipl/bin/swipl

:- set_prolog_flag(verbose, silent).
:- initialization main, halt.

:- use_module(stat).
:- use_module(plstat).

load_data_files(Files, [End|ByMonth]) :-
	maplist(read_file_to_terms, Files, NestTerms),
	append(NestTerms, AllTerms),
	partition(month_term, AllTerms, AllMonths, Ends),
	sort(Ends, AllEnds),
	last(AllEnds, End),
	map_list_to_pairs(month_key, AllMonths, Keyed),
	keysort(Keyed, SortedMonths),
	group_pairs_by_key(SortedMonths, Grouped),
	make_months(Grouped, ByMonth).

read_file_to_terms(File, Terms) :-
	read_file_to_terms(File, Terms, []).

month_term(downloads(_Y, _M, _C)).

month_key(downloads(Y, M, _C), Y/M).

make_months([], []).
make_months([_-List|T0], [M|T]) :-
	sum_counts(List, M),
	make_months(T0, T).

sum_counts([One], One) :- !.
sum_counts([H1,H2|T], Sum) :-
	sum_count(H1, H2, H),
	sum_counts([H|T], Sum).

sum_count(downloads(Y,M,Cs1),
	  downloads(Y,M,Cs2),
	  downloads(Y,M,Cs)) :-
	sort(Cs1, Css1),
	sort(Cs2, Css2),
	sum(Css1, Css2, Cs).

sum([], [], []) :- !.
sum(L,  [], L) :- !.
sum([], L,  L) :- !.
sum([N=V1|T1], [N=V2|T2], [N=V|T]) :- !,
	V is V1+V2,
	sum(T1, T2, T).
sum([N1=V1|T1], [N2=V2|T2], [N1=V1|T]) :-
	N1 @< N2, !,
	sum(T1, [N2=V2|T2], T).
sum(L1, [H|T2], [H|T]) :-
	sum(L1, T2, T).

sums(Options, Terms) :-
	option(sum_year(Y), Options), !,
	findall(C, member(downloads(Y, _, C), Terms), L),
	flatten(L, List),
	aggregate_all(sum(X), member(_=X, List), Count),
	format('Total downloads over ~d: ~D~n', [Y, Count]).
sums(_,_).


:- prolog_load_context(directory, Dir),
   working_directory(_, Dir).

main :- catch(run, E, print_message(error, E)).
run :-
	current_prolog_flag(argv, Argv),
	main_options(Argv, Options, Files),
	option(out(Out), Options, _),
	(   var(Out)
	->  Out = '../www/logs/swi-prolog-downloads.jpeg'
	;   true
	),
	load_data_files(Files, Terms),
	sums(Options, Terms),
	stat2jpeg(Out, Terms).

main_options(['-o', Out|T0], [out(Out)|OT], T) :- !,
	main_options(T0, OT, T).
main_options(['-s', YA|T0], [sum_year(Y)|OT], T) :- !,
	atom_number(YA, Y),
	main_options(T0, OT, T).
main_options(Files, [], Files).

